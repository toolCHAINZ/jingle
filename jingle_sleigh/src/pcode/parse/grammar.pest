WHITESPACE = _{ " " }
LABEL      =  { "<" ~ ASCII_DIGIT+ ~ ">" }

size  = @{ ASCII_DIGIT+ }
space = @{ ASCII_ALPHA+ }

// pcode display of varnodes
temporary = @{ "$U" ~ HEX_DIGIT+ }
const     = @{ (("0x" ~ HEX_DIGIT+) | ASCII_DIGIT+) }
register  = @{ (ASCII_ALPHANUMERIC | "_")+ }
// making the * optional because I think that's kind of
// insane syntax; def seems intended that direct references will almost
// always be to temporaries, constants, or registers
memory = ${ "*"? ~ "[" ~ space ~ "]" ~ const }

location = ${ temporary | const | memory }
varnode  = ${ (location ~ ":" ~ size) | register }

branch_dest = _{ LABEL | varnode }

quote   = _{ QUOTATION_MARK }
op_name = _{ (!quote ~ ASCII)+ }
string  = { quote ~ op_name ~ quote }

// memory ops
reference = ${ space ~ "(" ~ varnode ~ ")" }

callother_operation = { varnode | string }
callother_args      = { ("," ~ varnode)* }

// pcode ops
// Basic copy
COPY = { varnode ~ "=" ~ "COPY" ~ varnode }

// loads and stores
LOAD  = { varnode ~ "=" ~ "LOAD" ~ reference }
STORE = { "STORE" ~ reference ~ "," ~ varnode }

// control flow
BRANCH    = { "BRANCH" ~ branch_dest }
CBRANCH   = { "CBRANCH" ~ branch_dest ~ "," ~ varnode }
BRANCHIND = { "BRANCHIND" ~ varnode }
CALL      = { "CALL" ~ varnode }
CALLIND   = { "CALLIND" ~ varnode }
CALLOTHER = { (varnode ~ "=")? ~ "CALLOTHER" ~ callother_operation ~ callother_args }
RETURN    = { "RETURN" ~ varnode }

PIECE    = { varnode ~ "=" ~ "PIECE" ~ varnode ~ "," ~ varnode }
SUBPIECE = { varnode ~ "=" ~ "SUBPIECE" ~ varnode ~ "," ~ varnode }
POPCOUNT = { varnode ~ "=" ~ "POPCOUNT" ~ varnode }
LZCOUNT  = { varnode ~ "=" ~ "LZCOUNT" ~ varnode }

INT_EQUAL      = { varnode ~ "=" ~ "INT_EQUAL" ~ varnode ~ "," ~ varnode }
INT_NOTEQUAL   = { varnode ~ "=" ~ "INT_NOTEQUAL" ~ varnode ~ "," ~ varnode }
INT_LESS       = { varnode ~ "=" ~ "INT_LESS" ~ varnode ~ "," ~ varnode }
INT_SLESS      = { varnode ~ "=" ~ "INT_SLESS" ~ varnode ~ "," ~ varnode }
INT_LESSEQUAL  = { varnode ~ "=" ~ "INT_LESSEQUAL" ~ varnode ~ "," ~ varnode }
INT_SLESSEQUAL = { varnode ~ "=" ~ "INT_SLESSEQUAL" ~ varnode ~ "," ~ varnode }
INT_ZEXT       = { varnode ~ "=" ~ "INT_ZEXT" ~ varnode }
INT_SEXT       = { varnode ~ "=" ~ "INT_SEXT" ~ varnode }

// arithmetic / logical binary ops (dest = OP src1, src2)
INT_ADD     = { varnode ~ "=" ~ "INT_ADD" ~ varnode ~ "," ~ varnode }
INT_SUB     = { varnode ~ "=" ~ "INT_SUB" ~ varnode ~ "," ~ varnode }
INT_CARRY   = { varnode ~ "=" ~ "INT_CARRY" ~ varnode ~ "," ~ varnode }
INT_SCARRY  = { varnode ~ "=" ~ "INT_SCARRY" ~ varnode ~ "," ~ varnode }
INT_SBORROW = { varnode ~ "=" ~ "INT_SBORROW" ~ varnode ~ "," ~ varnode }
INT_2COMP   = { varnode ~ "=" ~ "INT_2COMP" ~ varnode }
INT_NEGATE  = { varnode ~ "=" ~ "INT_NEGATE" ~ varnode }

INT_XOR    = { varnode ~ "=" ~ "INT_XOR" ~ varnode ~ "," ~ varnode }
INT_AND    = { varnode ~ "=" ~ "INT_AND" ~ varnode ~ "," ~ varnode }
INT_OR     = { varnode ~ "=" ~ "INT_OR" ~ varnode ~ "," ~ varnode }
INT_LEFT   = { varnode ~ "=" ~ "INT_LEFT" ~ varnode ~ "," ~ varnode }
INT_RIGHT  = { varnode ~ "=" ~ "INT_RIGHT" ~ varnode ~ "," ~ varnode }
INT_SRIGHT = { varnode ~ "=" ~ "INT_SRIGHT" ~ varnode ~ "," ~ varnode }
INT_MULT   = { varnode ~ "=" ~ "INT_MULT" ~ varnode ~ "," ~ varnode }
INT_DIV    = { varnode ~ "=" ~ "INT_DIV" ~ varnode ~ "," ~ varnode }
INT_REM    = { varnode ~ "=" ~ "INT_REM" ~ varnode ~ "," ~ varnode }
INT_SDIV   = { varnode ~ "=" ~ "INT_SDIV" ~ varnode ~ "," ~ varnode }
INT_SREM   = { varnode ~ "=" ~ "INT_SREM" ~ varnode ~ "," ~ varnode }

BOOL_NEGATE = { varnode ~ "=" ~ "BOOL_NEGATE" ~ varnode }
BOOL_XOR    = { varnode ~ "=" ~ "BOOL_XOR" ~ varnode ~ "," ~ varnode }
BOOL_AND    = { varnode ~ "=" ~ "BOOL_AND" ~ varnode ~ "," ~ varnode }
BOOL_OR     = { varnode ~ "=" ~ "BOOL_OR" ~ varnode ~ "," ~ varnode }

// floating point ops
FLOAT_EQUAL     = { varnode ~ "=" ~ "FLOAT_EQUAL" ~ varnode ~ "," ~ varnode }
FLOAT_NOTEQUAL  = { varnode ~ "=" ~ "FLOAT_NOTEQUAL" ~ varnode ~ "," ~ varnode }
FLOAT_LESS      = { varnode ~ "=" ~ "FLOAT_LESS" ~ varnode ~ "," ~ varnode }
FLOAT_LESSEQUAL = { varnode ~ "=" ~ "FLOAT_LESSEQUAL" ~ varnode ~ "," ~ varnode }
FLOAT_ADD       = { varnode ~ "=" ~ "FLOAT_ADD" ~ varnode ~ "," ~ varnode }
FLOAT_SUB       = { varnode ~ "=" ~ "FLOAT_SUB" ~ varnode ~ "," ~ varnode }
FLOAT_MULT      = { varnode ~ "=" ~ "FLOAT_MULT" ~ varnode ~ "," ~ varnode }
FLOAT_DIV       = { varnode ~ "=" ~ "FLOAT_DIV" ~ varnode ~ "," ~ varnode }
FLOAT_NEG       = { varnode ~ "=" ~ "FLOAT_NEG" ~ varnode }
FLOAT_ABS       = { varnode ~ "=" ~ "FLOAT_ABS" ~ varnode }
FLOAT_SQRT      = { varnode ~ "=" ~ "FLOAT_SQRT" ~ varnode }
FLOAT_CEIL      = { varnode ~ "=" ~ "FLOAT_CIEL" ~ varnode }
FLOAT_FLOOR     = { varnode ~ "=" ~ "FLOAT_FLOOR" ~ varnode }
FLOAT_ROUND     = { varnode ~ "=" ~ "FLOAT_ROUND" ~ varnode }
FLOAT_NAN       = { varnode ~ "=" ~ "FLOAT_NAN" ~ varnode }

// conversion / cast
INT2FLOAT   = { varnode ~ "=" ~ "INT2FLOAT" ~ varnode }
FLOAT2FLOAT = { varnode ~ "=" ~ "FLOAT2FLOAT" ~ varnode }
TRUNC       = { varnode ~ "=" ~ "TRUNC" ~ varnode }

PCODE = {
    COPY
  | LOAD
  | STORE
  | BRANCH
  | CBRANCH
  | BRANCHIND
  | CALL
  | CALLIND
  | CALLOTHER
  | RETURN
  | PIECE
  | SUBPIECE
  | POPCOUNT
  | LZCOUNT
  | INT_EQUAL
  | INT_NOTEQUAL
  | INT_LESS
  | INT_SLESS
  | INT_LESSEQUAL
  | INT_SLESSEQUAL
  | INT_ZEXT
  | INT_SEXT
  | INT_ADD
  | INT_SUB
  | INT_CARRY
  | INT_SCARRY
  | INT_SBORROW
  | INT_2COMP
  | INT_NEGATE
  | INT_XOR
  | INT_AND
  | INT_OR
  | INT_LEFT
  | INT_RIGHT
  | INT_SRIGHT
  | INT_MULT
  | INT_DIV
  | INT_REM
  | INT_SDIV
  | INT_SREM
  | BOOL_NEGATE
  | BOOL_XOR
  | BOOL_AND
  | BOOL_OR
  | FLOAT_EQUAL
  | FLOAT_NOTEQUAL
  | FLOAT_LESS
  | FLOAT_LESSEQUAL
  | FLOAT_ADD
  | FLOAT_SUB
  | FLOAT_MULT
  | FLOAT_DIV
  | FLOAT_NEG
  | FLOAT_ABS
  | FLOAT_SQRT
  | FLOAT_CEIL
  | FLOAT_FLOOR
  | FLOAT_ROUND
  | FLOAT_NAN
  | INT2FLOAT
  | FLOAT2FLOAT
  | TRUNC
}

LINE  = _{ LABEL | PCODE }
LINES = _{ LINE ~ (NEWLINE+ ~ LINE)* }

PROGRAM = _{ SOI ~ LINES ~ NEWLINE* ~ EOI }
